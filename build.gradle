// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.21"
    ext.dagger2_version = "2.35.1"
    ext.room_version = "2.3.0"
    ext.retrofit_version = "2.9.0"
    ext.gson_version = "2.8.6"
    ext.spek_version = "2.0.12"
    repositories {
        google()
        mavenCentral()
        /*maven {
            url "https://plugins.gradle.org/m2/"
        }*/
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.17.1")
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    detekt {
        toolVersion = "1.17.1"
        input = files("$projectDir")
        config = files("$project.projectDir/config/detekt/detekt.yml")
        buildUponDefaultConfig = true
        reports {
            xml {
                enabled = true
                destination = file("$project.projectDir/detekt-report.xml")
            }
            html {
                enabled = true
                destination = file("$project.projectDir/detekt-report.html")
            }
            txt {
                enabled = true
                destination = file("$project.projectDir/detekt-report.txt")
            }
        }
    }

}

repositories {
    jcenter()
}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencies {
    ktlint "com.pinterest:ktlint:0.37.2"
}